\ group-dictionary.fs

REQUIRE ffl/hct.fs
REQUIRE small-string.fs

25000 CONSTANT GROUP-DICTIONARY-SIZE
GROUP-DICTIONARY-SIZE HCT-CREATE GROUP-DICTIONARY

27 CONSTANT MAX-LETTER
CREATE LETTERS-BUF MAX-LETTER ALLOT

: GROUP-DICTIONARY-LENGTH@ ( -- n )
    GROUP-DICTIONARY HCT-LENGTH@ ;

: UPDATE-NTH-GROUP ( ss,index -- )
    2DUP SMALL-STRING-C@ -ROT
    NTH-GROUP DUP
    SMALL-STRING-S
    GROUP-DICTIONARY HCT-GET 0= IF
        LS-EMPTY
    THEN
    ROT SWAP LS-ADD-CHAR
    SWAP SMALL-STRING-S
    GROUP-DICTIONARY HCT-INSERT ;

: GROUP-LETTERS@ ( ss -- add,l )
    SMALL-STRING-S
    GROUP-DICTIONARY HCT-GET DROP
    LS-CHARS ;

: WORD-EXIST? ( ss -- f )
    0 NTH-GROUP SMALL-STRING-S
    GROUP-DICTIONARY HCT-GET
    0= IF FALSE THEN ;
    
: ADD-WORD-GROUPS ( ss -- )
    DUP SMALL-STRING-LENGTH@ 0 ?DO
        DUP I UPDATE-NTH-GROUP
    LOOP DROP ;

100 CONSTANT MAX-LINE
CREATE LINE-BUFFER MAX-LINE ALLOT

: READ-WORDS ( adr,l -- )
    R/O OPEN-FILE THROW
    BEGIN
        DUP LINE-BUFFER MAX-LINE ROT
        READ-LINE THROW
    WHILE
        LINE-BUFFER SWAP
        SMALL-STRING ADD-WORD-GROUPS
    REPEAT
    DROP CLOSE-FILE THROW ;

